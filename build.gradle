plugins {
	id 'org.springframework.boot' version '4.0.0'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'application'
	id 'com.google.protobuf' version '0.9.6'
	id 'nebula.release' version '21.0.0'
	id 'jvm-test-suite'
}

repositories {
	mavenCentral()
}

group = 'com.github.timojakob'

ext {
	grpcVersion = "1.77.0"
	protoVersion = "4.33.0"
}


dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}

	// grpc (see https://github.com/grpc/grpc-java)
	implementation "io.grpc:grpc-netty-shaded:${grpcVersion}"
	implementation "io.grpc:grpc-protobuf:${grpcVersion}"
	implementation "io.grpc:grpc-stub:${grpcVersion}"

	implementation "com.google.protobuf:protobuf-java:${protoVersion}"
	implementation "com.google.protobuf:protobuf-java-util:${protoVersion}"

	compileOnly 'org.apache.tomcat:annotations-api:6.0.53' // necessary for Java 9+
}

protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:${protoVersion}"
	}
	plugins {
		grpc {
			artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
		}
	}
	generateProtoTasks {
		all()*.plugins {
			grpc {}
		}
	}
}

sourceSets {
	main {
		java {
			srcDirs 'build/generated/source/proto/main/grpc'
			srcDirs 'build/generated/source/proto/main/java'
		}
	}
}

testing {
	suites {
		test {
			useJUnitJupiter()
		}
	}
}
