plugins {
	id 'org.springframework.boot' version '2.7.1'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'application'
	id 'com.google.protobuf' version '0.8.18'
	id 'nebula.release' version '16.0.0'
}

repositories {
	mavenCentral()
}

group = 'com.github.timojakob'
sourceCompatibility = '17'

def grpcVersion = "1.47.0"
def protoVersion = "3.21.1"

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}

	// grpc (see https://github.com/grpc/grpc-java)
	implementation "io.grpc:grpc-netty-shaded:${grpcVersion}"
	implementation "io.grpc:grpc-protobuf:${grpcVersion}"
	implementation "io.grpc:grpc-stub:${grpcVersion}"

	compileOnly 'org.apache.tomcat:annotations-api:6.0.53' // necessary for Java 9+
}

protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:${protoVersion}"
	}
	plugins {
		grpc {
			artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
		}
	}
	generateProtoTasks {
		all()*.plugins {
			grpc {}
		}
	}
}

sourceSets {
	main {
		java {
			srcDirs 'build/generated/source/proto/main/grpc'
			srcDirs 'build/generated/source/proto/main/java'
		}
	}
}

test {
	useJUnitPlatform()
}
