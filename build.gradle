plugins {
	id 'java'

	// spring boot
	id 'org.springframework.boot' version '2.5.4'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'

	// protobuf & gRPC
	id 'com.google.protobuf' version '0.8.17'

	// testing & QA
	id "org.sonarqube" version "3.3"
	id 'jacoco'
}

repositories {
	mavenCentral()
}

group = 'com.github.timojakob'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '16'

def grpcVersion = "1.40.0"
def protoVersion = "3.17.3"

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}

	// grpc (see https://github.com/grpc/grpc-java)
	implementation "io.grpc:grpc-netty-shaded:${grpcVersion}"
	implementation "io.grpc:grpc-protobuf:${grpcVersion}"
	implementation "io.grpc:grpc-stub:${grpcVersion}"

	compileOnly 'org.apache.tomcat:annotations-api:6.0.53' // necessary for Java 9+
}

protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:${protoVersion}"
	}
	plugins {
		grpc {
			artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
		}
	}
	generateProtoTasks {
		all()*.plugins {
			grpc {}
		}
	}
}

sourceSets {
	main {
		java {
			srcDirs 'build/generated/source/proto/main/grpc'
			srcDirs 'build/generated/source/proto/main/java'
		}
	}
}

test {
	useJUnitPlatform()
}

sonarqube {
	properties {
		property "sonar.projectKey", "timo-jakob_tick-client-snapper"
		property "sonar.organization", "timo-jakob-github"
		property "sonar.host.url", "https://sonarcloud.io"
	}
}

jacocoTestReport {
	reports {
		xml.required = true
	}
}
